plugins {
    id 'java-library'
}

base {
    archivesName = "${mod_id}-${project.name}-1.20.5-1.20.6"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url 'https://raw.githubusercontent.com/Stein-N/resources/main/maven' }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url 'https://maven.terraformersmc.com/' }
    maven { url 'https://api.modrinth.com/maven' }
    maven { url 'https://maven.shedaniel.me' }
    maven { url 'https://cursemaven.com'}
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

processResources {
    var expandProps = [
            // Project related Keys
            "version": version,
            "group": project.group,

            // Mod related Keys
            "minecraft_version": minecraft_version,
            "mod_name": mod_name,
            "mod_author": mod_author,
            "mod_id": mod_id,
            "mod_license": mod_license,
            "mod_credits": mod_credits,
            "mod_description": mod_description,
            "minecraft_version_range": minecraft_version_range,

            // Fabric Keys
            "fabric_loader_version": fabric_loader_version,
            "fabric_api_version": fabric_api_version,

            // Forge Keys
            "forge_version": forge_version,
            "forge_loader_version_range": forge_loader_version_range,
            "forge_version_range": forge_version_range,

            // NeoForge Keys
            "neoforge_version": neoforge_version,
            "neoforge_loader_version_range": neoforge_loader_version_range,

            // Dependencies
            "resourceconfigapi": resource_config_api,
            "resourcelibrary": resource_library
    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}